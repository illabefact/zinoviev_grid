namespace word.count;

type LineStat = tuple<int32 lines, int32 words>;
type WordStat = tuple<rstring file, rstring word, int32 count>;

//getWordCount function splits the line into words and returns a map with count of each unique word in this line.
//returns map<rstring, int32>, where key is the word and value - its' count.
map<rstring, int32> getWordCount(rstring line)
{
	list<rstring> substrings = tokenize(line, " \t", false);
	mutable map<rstring, int32> uniqueWords = {};
	for (rstring s in substrings)
	{
		if (s in uniqueWords)
		{
			mutable int32 count = uniqueWords[s];
			count++;
			insertM(uniqueWords, s, count);
		}
		else
		{
			insertM(uniqueWords, s, 1);
		}
	}
	
	return uniqueWords;
}

//addStat function is used to cumulate statistics of words.
//input parameters:
//    map<rstring, int32> - already cumulated statistic
//    WordStat - tuple containing one word and its' count
void addStat(mutable map<rstring, int32> x, WordStat y) {
	if (y.word in x)
	{
		mutable int32 count = x[y.word];
		count = count + y.count;
		insertM(x, y.word, count);
	}
	else
	{
		insertM(x, y.word, y.count);
	}
} // End of function addStat
